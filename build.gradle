buildscript {
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.+'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'java-library'
    id "com.diffplug.spotless" version "5.0.0"
    id 'jacoco'
    id 'idea'
}

ext {
    versions = [
            awaitility      : '4.2.0',
            commonsLang     : '3.12.0',
            conductor       : '3.9.31-SNAPSHOT',
            jackson         : '2.11.4!!',
            junit           : '5.9.0',
            slf4j           : '1.7.36',
            spectator       : '1.3.7',
            awsSsm          : '1.12.300',
            azureSsm        : '4.2.3',
            azureIdentity   : '1.12.2',
            eureka          : '1.10.17',
            wiremock        : '2.33.2',
            ioGRPC          : '1.63.1',
            protoBuf        : '3.25.3',
            lombok          : '1.18.30',
            revJAXRS        : '2.1.1',
            log4j           : '2.17.1',
            netty           : '4.1.77.Final'

    ]
}

group = 'io.orkes.conductor'

dependencies {

    api ("io.orkes.conductor:conductor-common:${versions.conductor}")
    api ("io.orkes.conductor:conductor-grpc:${versions.conductor}")
    api ("io.orkes.conductor:conductor-java-sdk:${versions.conductor}") {
        exclude group: 'com.netflix.conductor'
    }
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${versions.jackson}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}"
    implementation "org.slf4j:slf4j-api:${versions.slf4j}"

    api 'io.orkes.conductor:orkes-conductor-common-protos:0.9.2'

    //gRPC dependencies
    implementation ("io.grpc:grpc-netty:${versions.ioGRPC}") {
        exclude module: "netty-common"
    }
    implementation "io.grpc:grpc-stub:${versions.ioGRPC}"
    implementation "com.google.protobuf:protobuf-java:${versions.protoBuf}"
    implementation "io.grpc:grpc-protobuf:${versions.ioGRPC}"
    implementation "io.grpc:grpc-stub:${versions.ioGRPC}"
    implementation "io.netty:netty-tcnative-boringssl-static:2.0.51.Final"

    //Kafka
    implementation 'org.apache.kafka:kafka-clients:3.3.1'

    //Misc
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
    compileOnly "org.projectlombok:lombok:${versions.lombok}"

    implementation "com.netflix.spectator:spectator-api:${versions.spectator}"
    implementation("com.netflix.eureka:eureka-client:${versions.eureka}") {
        exclude group: "javax.servlet", module: "servlet-api"
    }

    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.2'

    implementation "org.apache.commons:commons-lang3:${versions.commonsLang}"

    implementation 'org.threeten:threetenbp:1.6.1'

    //Integrations
    implementation "com.amazonaws:aws-java-sdk-ssm:${versions.awsSsm}"
    implementation "com.azure:azure-security-keyvault-secrets:${versions.azureSsm}"
    implementation "com.azure:azure-identity:${versions.azureIdentity}"

    //logging
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}!!"
    implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}!!"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}!!"
    implementation "org.apache.logging.log4j:log4j-jul:${versions.log4j}!!"
    implementation "org.apache.logging.log4j:log4j-web:${versions.log4j}!!"

    //spring
    implementation "org.springframework:spring-context:5.3.24"

    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    // test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.testcontainers:localstack:1.17.1'
    testImplementation 'org.testcontainers:testcontainers:1.17.1'
    testImplementation 'com.amazonaws:aws-java-sdk-core:1.12.138'
    testCompileOnly "org.projectlombok:lombok:${versions.lombok}"

    //Spring
    testImplementation 'org.springframework.boot:spring-boot-starters:2.2.13.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-autoconfigure:2.7.8'
    testImplementation 'org.springframework.boot:spring-boot-test:2.7.8'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    exclude 'io/orkes/conductor/sdk/examples'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Orkes Conductor Client'
                description = 'Netflix Conductor client that provides support for Orkes Conductor features'
                url = 'https://github.com/orkes-io/orkes-conductor-client'
                scm {
                    connection = 'scm:git:git://github.com/orkes-io/orkes-conductor-client.git'
                    developerConnection = 'scm:git:ssh://github.com:orkes-io/orkes-conductor-client.git'
                    url = 'https://github.com/orkes-io/orkes-conductor-client'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        organization = 'Orkes'
                        organizationUrl = 'https://orkes.io'
                        name = 'Orkes Development Team'
                        email = 'developers@orkes.io'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if (project.hasProperty("mavenCentral")) {
                println "Publishing to Sonatype Repository"
                url = "https://s01.oss.sonatype.org/${project.version.endsWith('-SNAPSHOT') ? "content/repositories/snapshots/" : "service/local/staging/deploy/maven2/"}"
                credentials {
                    username project.properties.username
                    password project.properties.password
                }
            } else {
                url = "s3://orkes-artifacts-repo/${project.version.endsWith('-SNAPSHOT') ? "snapshots" : "releases"}"
                authentication {
                    awsIm(AwsImAuthentication)
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
    testLogging {
        events = ["SKIPPED", "FAILED"]
        exceptionFormat = "full"
        showStandardStreams = true
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

configurations.implementation {
    exclude(group : "com.netflix.conductor")
}

configurations {
    all {
        //exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'com.netflix.conductor'
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    }
}

spotless {
    java {
        removeUnusedImports()
        importOrder('java', 'javax', 'org', 'com.netflix', 'io.orkes','', '\\#com.netflix', '\\#')
        licenseHeaderFile("$rootDir/licenseheader.txt")
    }
}

signing {
    def signingKeyId = findProperty('signingKeyId')
    if (signingKeyId) {
        println 'Signing the artifact with keys'
        signing {
            def signingKey = findProperty('signingKey')
            def signingPassword = findProperty('signingPassword')
            if (signingKeyId && signingKey && signingPassword) {
                useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
            }

            sign publishing.publications
        }
    }
}
tasks.withType(Test)  {
    maxParallelForks = 1
}
//sourceSets.main.java.srcDirs += ['examples/java', 'examples/resources']

